#!/usr/bin/env perl
# finddisappeared - find files that are owned by package manager but disappeared
# See COPYING and COPYRIGHT files for corresponding information.

use strict;
use warnings;
use File::Basename;
use Getopt::Long qw(:config gnu_compat);

# Program name and version.
my $PROGRAM = basename $0;
my $VERSION = '@VERSION@';

######################################################################
# Finddisappeared subroutines.                                       #
######################################################################

# Find files that are listed in the package database but are
# disappeared from the filesystem.
sub find_disappeared_files {
    my $root = shift || '';
    my $db = "$root/var/lib/pkg/db";

    open my $fh, '<', $db
        or die "$PROGRAM: error: could not open package database ($db): $!\n";

    local $/ = ''; # Read files paragraph wise.
    while (<$fh>) {
        my ($name, undef, @files) = split /\n/;
        for my $file (@files) {
            print "$name: /$file\n" unless -e "$root/$file";
        }
    }
    close $fh;
}

######################################################################
# Command-line helpers.                                              #
######################################################################

sub print_version {
    print "$PROGRAM (pkgmaint) $VERSION\n";
}

sub print_help {
    print <<EOF;
Usage: $PROGRAM [-hv] [-r rootdir]
Find files that are owned by package manager but somehow disappeared.

Mandatory arguments to long options are mandatory for short options too.
  -r, --root=rootdir  specify an alternate root directory
  -v, --version       print version and exit
  -h, --help          print help and exit
EOF
}

sub main {
    my $usage_message = "Try '$PROGRAM --help' for more information.";

    GetOptions(
        "r|root=s"  => \my $opt_root,
        "v|version" => \my $opt_version,
        "h|help"    => \my $opt_help,
    ) or die "$usage_message\n";

    print_version() and exit if $opt_version;
    print_help()    and exit if $opt_help;

    die "$PROGRAM: invalid option -- '@ARGV'\n$usage_message\n" if @ARGV;

    find_disappeared_files $opt_root;
}

######################################################################

# Run main if not loaded as module.
main() if not caller();

1;

# vim: sw=4 ts=4 sts=4 et cc=72 tw=70
# End of file.
