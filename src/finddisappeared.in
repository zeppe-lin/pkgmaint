#!/usr/bin/env perl
# finddisappeared - find files that are owned by package manager but disappeared
# See COPYING and COPYRIGHT files for corresponding information.

use strict;
use warnings;
use File::Basename;
use Getopt::Long qw(:config gnu_compat no_auto_abbrev no_ignore_case);

# Program name and version.
my $PROGRAM = basename $0;
my $VERSION = '@VERSION@';

# Default path to the package database.
my $DB_PATH = '/var/lib/pkg/db';

######################################################################
# Core functionality                                                 #
######################################################################

# Find files that are listed in the package database but are
# disappeared from the filesystem.
sub find_disappeared_files {
    my $root = shift || '';
    my $db = $root . $DB_PATH;

    open my $fh, '<', $db
        or die "Could not open package database ($db): $!\n";

    local $/ = ''; # paragraph mode
    while (<$fh>) {
        my ($name, undef, @files) = split /\n/;
        for my $file (@files) {
            print "$name: /$file\n" unless -e "$root/$file";
        }
    }
    close $fh;
}

######################################################################
# CLI helpers                                                        #
######################################################################

sub print_version {
    print "$PROGRAM (pkgmaint) $VERSION\n";
}

sub print_help {
    print <<EOF;
Usage: $PROGRAM [-hv] [-r root-dir]
Find files that are owned by package manager but somehow disappeared.

Mandatory arguments to long options are mandatory for short options too.
  -r, --root=root-dir  Use an alternate root directory
  -v, --version        Print version and exit
  -h, --help           Print this help and exit
EOF
}

######################################################################
# Entry point                                                        #
######################################################################

sub main {
    GetOptions(\my %opt, 'root|r=s', 'version|v', 'help|h')
        or die "Try '$PROGRAM --help' for more information.\n";

    print_version() and exit if $opt{version};
    print_help()    and exit if $opt{help};

    die "Unknown option: @ARGV\nTry '$PROGRAM --help' for more information.\n"
        if @ARGV;

    find_disappeared_files $opt{root};
}

######################################################################

# Run main if not loaded as module.
main() if not caller();

1;

# vim: sw=4 ts=4 sts=4 et cc=72 tw=70
# End of file.
